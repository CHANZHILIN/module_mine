apply from: "${rootProject.rootDir}/common_config.gradle"    //引用额外的配置
if (project.ext.isModuleMineDebug) {
    apply plugin: 'com.android.application'  //组件开发模式，独立运行
} else {
    apply plugin: 'com.android.library'  //集成开发模式
}

android {
    project.ext.setDefaultConfig android    //调用配置信息

    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            if (project.ext.isModuleMineDebug) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {  //排除module下的所有文件
                    exclude 'src/main/module/*'
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:${project.ext.supportVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.1'
    implementation project(':lib_base')
}
